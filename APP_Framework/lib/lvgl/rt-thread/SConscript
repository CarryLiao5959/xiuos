from building import *
import rtconfig
import os
import shutil

# get current dir path
cwd = GetCurrentDir()

src = []
inc = []
src += ['../porting/lv_port_disp_template.c']
src += ['../porting/lv_port_fs_template.c']
src += ['../porting/lv_port_indev_template.c']
lvgl_cwd = cwd + '/../'
lvgl_src_cwd = lvgl_cwd + 'src/'

inc = inc + [lvgl_src_cwd]

for root, dirs, files in os.walk(lvgl_src_cwd):
    for dir in dirs:
        src = src + Glob(os.path.join(root,dir,'*.c'))
        inc = inc + [os.path.join(root,dir)]

inc = inc + [lvgl_cwd]
LOCAL_CCFLAGS = ''
if rtconfig.PLATFORM == 'gcc': # GCC
    LOCAL_CCFLAGS += ' -std=c99'
elif rtconfig.PLATFORM == 'armcc': # Keil AC5
    LOCAL_CCFLAGS += ' --c99 --gnu -g -W'
elif rtconfig.PLATFORM == 'armclang': # Keil AC6
    LOCAL_CCFLAGS += ' -std=c99 -g -w'

group = DefineGroup('LVGL', src, depend = ['LIB_LV'], CPPPATH = inc, LOCAL_CCFLAGS = LOCAL_CCFLAGS)

port_src = Glob('*.c')
port_inc = [cwd]+['../porting/']
group = group + DefineGroup('LVGL-port', port_src, depend = ['LIB_LV'], CPPPATH = port_inc)

list = os.listdir(cwd)
for d in list:
    path = os.path.join(cwd, d)
    if os.path.isfile(os.path.join(path, 'SConscript')):
        group = group + SConscript(os.path.join(d, 'SConscript'))
Return('group')
